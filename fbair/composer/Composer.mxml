<!--
  Copyright Facebook Inc.

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
 -->
<!-- There's one of these at the top-level.
     Major container for letting the user share stuff! -->
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
         xmlns:util="fbair.util.display.*" >

  <mx:Metadata>
    [Event(name="statusUpdated", type="fb.FBEvent")]
  </mx:Metadata>

  <!-- Text area for entering your status update -->
  <util:GrowableTextArea id="composerInput"
                         styleName="composerInput"
                         width="100%"
                         focusOutText="What's on your mind?"
                         focusInHeight="{
                           StylePrefs.sizeStyle == StylePrefs.SIZE_LARGE ? 
                             48 : 40}"
                         focusOutHeight="{
                           StylePrefs.sizeStyle == StylePrefs.SIZE_LARGE ?
                             29 : 25}" />

  <util:FBButton id="shareButton"
                 autoStyle="true"
                 click="shareStatus(event)" >
    <mx:Label styleName="fbButtonLabel"
              text="Share" />
  </util:FBButton>

  <mx:Script><![CDATA[
    import fb.FBAPI;
    import fb.FBConnect;
    import fb.FBEvent;
    import fb.util.Output;

    import fbair.gui.StylePrefs;
    import fbair.nile.NileContainer;

    import flash.events.MouseEvent;

    import mx.core.Application;

    private var submittedMessage:String;

    // Called when the Share button has been clicked
    private function shareStatus(event:MouseEvent):void {
      if (event.shiftKey) {
        Output.logDump();
        return;
      }

      if (!composerInput.active) {
        Application.application.focusManager.setFocus(composerInput);
        return;
      }

      // Don't share a blank string!
      if (composerInput.text.length == 0) return;

      // Disenable
      submittedMessage = composerInput.text;
      composerInput.active = false;
      composerInput.editable = false;
      shareButton.enabled = false;

      // Stream.publish mofo!
      var publish_options:Object = {message:submittedMessage};
      if (NileContainer.FilterIsPage)
        publish_options.uid = NileContainer.CurrentFilter;
        
      FBAPI.callMethod("stream.publish", publish_options)
        .addEventListener(FBEvent.SUCCESS, statusUpdated);
    }

    // Called when the server has verified status.set
    private function statusUpdated(event:FBEvent):void {
      if (!submittedMessage) return;

      // Dispatch event that we've updated our status
      dispatchEvent(new FBEvent("statusUpdated", {
        post_id:event.data,
        actor_id:FBConnect.session.uid,
        app_id:ApplicationBase.AppID,
        message:submittedMessage,
        likes:{count:0, user_likes:false, can_like:true},
        comments:{count:0, posts:[], can_post:true, can_remove:true},
        created_time:(new Date().time / 1000),
        is_page:NileContainer.FilterIsPage,
        filter_key:NileContainer.CurrentFilter,
        attachment:{}
      }));

      // Enable
      submittedMessage = null;
      composerInput.text = "";
      composerInput.editable = true;
      shareButton.enabled = true;
    }
  ]]></mx:Script>
</mx:HBox>
